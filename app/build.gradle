apply plugin: 'com.android.application'

android {
    compileSdkVersion 29
    buildFeatures.dataBinding = true

    defaultConfig {
        applicationId "xyz.zood.george"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 34
        versionName "0.34"
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        resConfigs "en"
        vectorDrawables.useSupportLibrary = true
        externalNativeBuild {
            cmake {
                cFlags "-Os -fvisibility=hidden"
            }
        }
        ndk {
            abiFilters 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
//            abiFilters 'armeabi-v7a', 'arm64-v8a'
        }
    }
    buildTypes {
        release {
            shrinkResources false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            jniDebuggable = true
            renderscriptDebuggable = false
        }
    }
    sourceSets {
        main {
            jniLibs.srcDirs = ['../sodium/lib']
        }
    }
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    ndkVersion '20.0.5594570'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'com.google.android.material:material:1.3.0-alpha01'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    // dynamic animation
    implementation 'androidx.dynamicanimation:dynamicanimation:1.0.0'

    // ViewPager2
    implementation "androidx.viewpager2:viewpager2:1.1.0-alpha01"

    // used for fusion location
    implementation 'com.google.android.gms:play-services-location:17.0.0'
    // used for Google maps view
    implementation 'com.google.android.gms:play-services-maps:17.0.0'
    // cloud messaging
    implementation ('com.google.firebase:firebase-messaging:20.2.1', {
        exclude group: 'com.google.firebase', module: 'firebase-core'
        exclude group: 'com.google.firebase', module: 'firebase-analytics'
        exclude group: 'com.google.firebase', module: 'firebase-measurement-connector'
    })

    implementation 'com.squareup.retrofit2:retrofit:2.8.2'
    implementation 'com.squareup.retrofit2:converter-gson:2.8.2'

//    implementation 'com.squareup.okhttp3:okhttp:3.14.9'
//    implementation 'com.squareup.okhttp3:logging-interceptor:3.12.1'

    implementation 'com.squareup.picasso:picasso:2.5.2'

    implementation 'com.theartofdev.edmodo:android-image-cropper:2.8.0'

    // jetpack libs
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"

    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
}

apply plugin: 'com.google.gms.google-services'
